plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.119'
    id 'checkstyle'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = project.mod_version
group = project.mod_group_id

repositories {
    mavenLocal()
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

base {
    archivesName = project.mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

checkstyle {
    toolVersion = '10.12.4'
    configFile = file('config/checkstyle/checkstyle.xml')
}

sourceSets {
    tinyUi {
        java {
            srcDir 'src/main/java'
            include 'com/Kizunad/tinyUI/**'
        }
        resources {
            srcDir 'src/main/resources'
        }
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += output + compileClasspath
    }
    tinyUiTest {
        java {
            srcDir 'src/test/java'
            include 'com/Kizunad/tinyUI/**'
        }
        resources {
            srcDir 'src/test/resources'
        }
        compileClasspath += sourceSets.tinyUi.output + configurations.testCompileClasspath
        runtimeClasspath += output + compileClasspath + configurations.testRuntimeClasspath
    }
}

configurations {
    tinyUiImplementation.extendsFrom implementation
    tinyUiTestImplementation.extendsFrom testImplementation
    tinyUiTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('tinyUiTest', Test) {
    description = 'Run tinyUI tests without compiling non-tinyUI sources.'
    group = 'verification'
    testClassesDirs = sourceSets.tinyUiTest.output.classesDirs
    classpath = sourceSets.tinyUiTest.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter(test)
}

tasks.register('tinyUiJar', Jar) {
    description = 'Package tinyUI classes into a standalone jar.'
    group = 'build'
    from(sourceSets.tinyUi.output) {
        exclude 'META-INF/neoforge.mods.toml' // 将在任务内写入专用 toml
    }
    archiveClassifier = 'tinyui'
    dependsOn tasks.named('tinyUiClasses')
    dependsOn tasks.named('processResources')

    // tinyui jar 不需要 customnpcs 代码/资源
    exclude 'com/Kizunad/customNPCs/**'
    exclude 'assets/customnpcs/**'
    exclude 'data/customnpcs/**'

    // 写入只包含 tinyui 的 mods.toml
    def processedToml = layout.buildDirectory.file("resources/main/META-INF/neoforge.mods.toml")
    def tinyTomlContent = providers.provider {
        def toml = processedToml.get().asFile.text
        def parts = toml.split('\\[\\[mods\\]\\]')
        if (parts.size() < 2) {
            throw new GradleException("neoforge.mods.toml 格式异常，未找到 [[mods]] 块")
        }
        def header = parts[0]
        def blocks = parts[1..-1]
        def tinyBlock = blocks.find { it.contains('modId="tinyui"') }
        if (tinyBlock == null) {
            throw new GradleException("未找到 tinyui 的 mods 声明")
        }
        return header + '[[mods]]' + tinyBlock
    }
    def tinyTomlFile = layout.buildDirectory.file("tmp/tinyui/neoforge.mods.toml")
    inputs.property("tinyuiToml", tinyTomlContent)
    from(tinyTomlFile) {
        into 'META-INF'
        rename { 'neoforge.mods.toml' }
    }
    doFirst {
        def f = tinyTomlFile.get().asFile
        f.parentFile.mkdirs()
        f.text = tinyTomlContent.get()
    }
}

tasks.register('customNpcsJar', Jar) {
    description = 'Package CustomNPCs classes into a standalone jar.'
    group = 'build'
    // 保留 mod 必要的元数据/资源, 仅打包 CustomNPCs 代码
    from(sourceSets.main.output) {
        include 'com/Kizunad/customNPCs/**'
        include 'META-INF/**'
        include 'assets/**'
        include 'data/**'
        include 'pack.mcmeta'
        exclude 'META-INF/neoforge.mods.toml' // 将在任务内写入专用 toml
    }
    archiveClassifier = 'customnpcs'
    dependsOn tasks.named('classes')
    dependsOn tasks.named('processResources')

    // customnpcs jar 不需要 tinyui 代码/资源
    exclude 'com/Kizunad/tinyUI/**'
    exclude 'assets/tinyui/**'
    exclude 'data/tinyui/**'

    // 写入只包含 customnpcs 的 mods.toml
    def processedToml = layout.buildDirectory.file("resources/main/META-INF/neoforge.mods.toml")
    def customTomlContent = providers.provider {
        def toml = processedToml.get().asFile.text
        def parts = toml.split('\\[\\[mods\\]\\]')
        if (parts.size() < 2) {
            throw new GradleException("neoforge.mods.toml 格式异常，未找到 [[mods]] 块")
        }
        def header = parts[0]
        def blocks = parts[1..-1]
        def customBlock = blocks.find { it.contains('modId="customnpcs"') }
        if (customBlock == null) {
            throw new GradleException("未找到 customnpcs 的 mods 声明")
        }
        return header + '[[mods]]' + customBlock
    }
    def customTomlFile = layout.buildDirectory.file("tmp/customnpcs/neoforge.mods.toml")
    inputs.property("customnpcsToml", customTomlContent)
    from(customTomlFile) {
        into 'META-INF'
        rename { 'neoforge.mods.toml' }
    }
    doFirst {
        def f = customTomlFile.get().asFile
        f.parentFile.mkdirs()
        f.text = customTomlContent.get()
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = '2024.11.17'
        minecraftVersion = '1.21.1'
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        "${project.mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : project.minecraft_version,
            minecraft_version_range: project.minecraft_version_range,
            neo_version         : project.neo_version,
            neo_version_range   : project.neo_version_range,
            loader_version_range: project.loader_version_range,
            mod_id              : project.mod_id,
            mod_name            : project.mod_name,
            mod_license         : project.mod_license,
            mod_version         : project.mod_version,
            mod_authors         : project.mod_authors,
            mod_description     : project.mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}
